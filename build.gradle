buildscript {
    /* Репозитории Gradle-плагинов */
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            name = "forge"
            url = uri("https://maven.minecraftforge.net")
        }
        maven {
            name = "jitpack"
            url = uri("https://jitpack.io")
        }
        maven {
            name = "github"
            url = uri("https://github.com/juanmuscaria/maven/raw/master")
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
           changing = true
       }
    }
}

repositories {
    flatDir {
        dirs 'deps'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'forge'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

/* Вспомогательные конфиги для IDEA */
idea {
    module {
        inheritOutputDirs = true // Складываем *.class и assets в одну директорию, из разных Forge читать не будет
        excludeDirs += file('./run') // Исключаем директории из индексации
    }
}

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10' // Версия Forge
    //mappings = "stable_12" // Версия маппингов Minecraft для подготовки среды разработки
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Dev версия мода без обфускации MC */
tasks.register('devJar', Jar) {
    archiveClassifier.set('dev')
    manifest.from(jar.manifest)
    from sourceSets.main.output
}

artifacts {
    archives devJar
}
