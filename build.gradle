buildscript {
    /* Репозитории Gradle-плагинов */
    repositories {
        mavenCentral()
        maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:[1.2-1.4.6-SNAPSHOT,)'
    }
}

repositories {
    flatDir {
        dirs 'deps'
    }
}

apply plugin: 'java'
apply plugin: 'forge'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

/* Вспомогательные конфиги для IDEA */
idea {
    module {
        inheritOutputDirs = true // Складываем *.class и assets в одну директорию, из разных Forge читать не будет
        excludeDirs += file('./run') // Исключаем директории из индексации
    }
}

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10' // Версия Forge
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Dev версия мода без обфускации MC */
task devJar(type: Jar) {
    classifier = 'dev'
    manifest = jar.manifest
    from sourceSets.main.output
}

artifacts {
    archives devJar
}
